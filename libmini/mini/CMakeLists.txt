# cmake build file for libMini
# (c) by Stefan Roettger
#
# usage:
#
#    1: cmake .
#    2: cmake -DLIBMINI_THIRDPARTY_DIR="..." .
#    3: setenv LIBMINI_THIRDPARTY_DIR "..."; cmake .
#    4: cmake -D<libname>_LIBRARY="..." -D<libname>_INCLUDE_DIR="..." .
#       where <libname> is any of {PTHREAD,OPENTHREADS,CURL,JPEG,PNG,ZLIB,SQUISH,GREYCSTORATION,GLUT}
#
# optional usage examples:
#
#    cmake -DBUILD_TYPE=Debug .
#    cmake -DBUILD_TYPE=Release .
#    cmake -DBUILD_SHARED_LIBS=ON .
#    cmake -DCMAKE_VERBOSE_MAKEFILE=TRUE . (cmake .; make VERBOSE=1)
#    cmake -DCMAKE_INSTALL_PREFIX=/usr/local .

PROJECT(libMini)

CMAKE_MINIMUM_REQUIRED(VERSION 2.6)

OPTION(BUILD_SFX "Build SFX library." OFF)
OPTION(BUILD_APPS "Build applications." OFF)
OPTION(BUILD_TOOLS "Build command line tools." OFF)

INCLUDE(${CMAKE_CURRENT_SOURCE_DIR}/libMini-config.cmake)

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR})

ADD_LIBRARY(${MINI_NAME} ${MINI_SRCS} ${MINI_HDRS})
SET_TARGET_PROPERTIES(${MINI_NAME} PROPERTIES DEBUG_POSTFIX "d")
TARGET_LINK_LIBRARIES(${MINI_NAME} ${OPENGL_LIBRARIES})

IF (BUILD_SFX OR BUILD_APPS OR BUILD_TOOLS)
   ADD_LIBRARY(${MINISFX_NAME} ${MINISFX_SRCS} ${MINISFX_HDRS})
   SET_TARGET_PROPERTIES(${MINISFX_NAME} PROPERTIES DEBUG_POSTFIX "d")
   TARGET_LINK_LIBRARIES(${MINISFX_NAME}
      ${MINI_NAME}
      ${OPENGL_LIBRARIES}
      ${THREAD_LIBRARY}
      ${CURL_LIBRARY}
      ${JPEG_LIBRARY}
      ${PNG_LIBRARY}
      ${ZLIB_LIBRARY}
      ${SQUISH_LIBRARY}
      )
ENDIF (BUILD_SFX OR BUILD_APPS OR BUILD_TOOLS)

MACRO(MAKE_MINI_EXECUTABLE name)
   ADD_EXECUTABLE(${name} ${name}.cpp)
   TARGET_LINK_LIBRARIES(${name}
      ${MINI_NAME}
      ${OPENGL_LIBRARIES}
      ${GLUT_LIBRARY}
      )
   IF (WIN32)
      TARGET_LINK_LIBRARIES(${name} ws2_32)
   ENDIF (WIN32)
ENDMACRO(MAKE_MINI_EXECUTABLE)

MACRO(MAKE_MINISFX_EXECUTABLE name)
   ADD_EXECUTABLE(${name} ${name}.cpp)
   TARGET_LINK_LIBRARIES(${name}
      ${MINI_NAME}
      ${MINISFX_NAME}
      ${OPENGL_LIBRARIES}
      ${THREAD_LIBRARY}
      ${CURL_LIBRARY}
      ${JPEG_LIBRARY}
      ${PNG_LIBRARY}
      ${ZLIB_LIBRARY}
      ${SQUISH_LIBRARY}
      ${GLUT_LIBRARY}
      )
   IF (WIN32)
      TARGET_LINK_LIBRARIES(${name} ws2_32)
   ENDIF (WIN32)
ENDMACRO(MAKE_MINISFX_EXECUTABLE)

IF (BUILD_APPS)
   MAKE_MINI_EXECUTABLE(example)
   MAKE_MINISFX_EXECUTABLE(viewer)
ENDIF (BUILD_APPS)

IF (BUILD_TOOLS)
   ADD_SUBDIRECTORY(tools)
ENDIF (BUILD_TOOLS)

INSTALL(
   TARGETS ${MINI_NAME}
   LIBRARY DESTINATION lib
   ARCHIVE DESTINATION lib
   )
INSTALL(
   FILES ${MINI_HDRS}
   DESTINATION include/mini
   )

IF (BUILD_SFX)
   INSTALL(
      TARGETS ${MINISFX_NAME}
      LIBRARY DESTINATION lib
      ARCHIVE DESTINATION lib
      )
   INSTALL(
      FILES ${MINISFX_HDRS}
      DESTINATION include/mini
      )
ENDIF (BUILD_SFX)

IF (BUILD_APPS)
   INSTALL(
      TARGETS viewer
      RUNTIME DESTINATION bin
      )
ENDIF (BUILD_APPS)
